#!/usr/bin/env just --justfile

top := env_var('TOP')
jpath := canonicalize(source_file())
jdir := parent_directory(jpath)

set shell := ["bash", "-c"]

@default:
    just --list

[private]
_checkout +ARGS='':
    git checkout {{ ARGS }}

[private]
_pull +ARGS='':
    git pull --rebase --autostash {{ ARGS }}
    touch "$PWD/.envrc"

[private]
_commit_no_message +ARGS='': (check ARGS)
    git commit {{ ARGS }}

[private]
_commit_message MESSAGE +ARGS='': (_commit_no_message ARGS '-m' quote(MESSAGE))

[private]
_add +ARGS='':
    git add {{ ARGS }}

check +ARGS='':
    git commit --dry-run {{ ARGS }}

commit MESSAGE +ARGS='': (_commit_message MESSAGE ARGS)

amend +ARGS='': (_add '-A') (_commit_no_message '--amend --no-edit --reuse-message=HEAD' ARGS)

rebase BRANCH=("main") +ARGS='': (_checkout BRANCH) _pull (_checkout '-')
    git rebase --autostash {{ BRANCH }} {{ ARGS }}

